00 ' Pixel November 04
01 ' Fractal Tree
02 ' https://rosettacode.org/wiki/Fractal_tree#BASIC

05 MYTITLE$ = "Fractal Tree"

09 CLS
10 SCREEN 2,0,0
11 ATTRB 1,1
12 LOCATE (40-LEN(MYTITLE$)*2)/2-1, 6, 0
13 PRINT MYTITLE$

14 ATTRB 0,1
15 LOCATE 11, 12, 0
16 PRINT "Pixel November 04"

17 LOCATE 15, 15, 0
18 ATTRB 0,0
19 PRINT "by FloGom"

20 ' Start new seed for random function
21 ATTRB 0,0
22 LOCATE 8, 22, 0
23 PRINT "Push any key to start."
24 A$ = INKEY$
25 U = RND
26 IF A$="" THEN 24

30 CLS
35 SCREEN 2,0,0

40 WINW = 300
45 WINH = 180

50 LEVEL=9
60 LONG = 40
70 X = WINW/2 : Y = WINH
80 PI = 3.1415
85 ROTATION = PI/2

90 A1 = PI/27 : A2 = PI/8
95 C1 = 0.8 : C2 = 0.85

100 DIM XS(LEVEL+1) : DIM YS(LEVEL+1)
120 COLOR 2
125 GOSUB 510

495 A$=INKEY$
496 IF A$="" THEN 495
497 CLS

499 END

500 ' tree program
510 XS(LEVEL) = X : YS(LEVEL) = Y
515 GOSUB 600 'put line
520 IF LEVEL>0 THEN 525 ELSE 580
525 LEVEL = LEVEL -1
530 LONG = LONG * C1
535 ROTATION = ROTATION - A1
540 GOSUB 510 
545 LONG = LONG/C1*C2
550 ROTATION = ROTATION +A1 +A2
555 GOSUB 510
560 ROTATION = ROTATION - A2
565 LONG = LONG/C2
570 LEVEL = LEVEL + 1
580 X = XS(LEVEL) : Y=YS(LEVEL)
590 RETURN

600 YN = -1*SIN(ROTATION)*LONG + Y
610 XN = COS(ROTATION)*LONG + X
620 LINE(X,Y)-(XN, YN)
630 X = XN : Y = YN
640 RETURN
