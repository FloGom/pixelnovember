00 ' Pixel November 07
01 ' Template
02 ' https://en.wikipedia.org/wiki/B%C3%A9zier_curve

05 MYTITLE$ = "Bezier Dreams"

06 BSTEP = 25
07 DIM XB1%(BSTEP) : DIM YB1%(BSTEP)
08 DIM XB2%(BSTEP) : DIM YB2%(BSTEP)
09 TSTEP = 1/BSTEP



10 CLS
11 SCREEN 7,0,0
12 ATTRB 1,1
13 LOCATE (40-LEN(MYTITLE$)*2)/2-1, 6, 0
14 PRINT MYTITLE$

15 ATTRB 0,1
16 LOCATE 11, 12, 0
17 PRINT "Pixel November 07"

18 LOCATE 15, 15, 0
19 ATTRB 0,0
20 PRINT "by FloGom"

30 ' Start new seed for random function
31 ATTRB 0,0
32 LOCATE 8, 22, 0
33 PRINT "Push any key to start."
34 A$ = INKEY$
35 U = RND
36 IF A$="" THEN 34

37 CLS
38 'CLS
39 fcol=CINT(RND*14)+1
40 COUNT = 0

50 XPSTART = FIX(RND*329) : YP1 = 0

51 IF COUNT = 0 THEN 52 ELSE 61
52 XP1 = XPSTART
53 XP2 = FIX(RND*329) : YP2 = FIX(RND*199)
54 XP3 = FIX(RND*329) : YP3 = 199
55 GOSUB 1500
56 XP2 = FIX(RND*329) : YP2 = FIX(RND*199)
57 XP3 = FIX(RND*329)
58 GOSUB 1600
59 CLS
60 GOTO 86

61 IF COUNT = 1 THEN XP1 = XP3 : YP1 = YP3 ELSE 75
62 XP2 = FIX(RND*329) : YP2 = FIX(RND*199)
63 IF RND > 0.5 THEN 64 ELSE 69
64 XP3 = 0 : YP3 = FIX(RND*199)
65 GOSUB 1500
66 XP2 = FIX(RND*329) : YP2 = FIX(RND*199) : YP3 = FIX(RND*199)
67 GOSUB 1600
68 GOTO 86
69 XP3 = 319 : YP3 = FIX(RND*199)
70 GOSUB 1500
71 XP2 = FIX(RND*329) : YP2 = FIX(RND*199) : YP3 = FIX(RND*199)
72 GOSUB 1600
73 GOTO 86

75 IF COUNT = 2 THEN 76 ELSE 38
76 XP1 = XP3 : YP1 = YP3 : XP3 = XPSTART : YP3 = 0
77 XP2 = FIX(RND*329) : YP2 = FIX(RND*199)
78 GOSUB 1500
79 XP2 = FIX(RND*329) : YP2 = FIX(RND*199)
80 GOSUB 1600

86 COUNT = COUNT + 1

87 GOSUB 1700

90 GOTO 51


495 A$=INKEY$
496 IF A$="" THEN 495
497 CLS

500 END

1500 ' store B1 curve
1510 FOR T=0 TO BSTEP
1520 XB1%(T) = (1-T*TSTEP)*((1-T*TSTEP)*XP1 + T*TSTEP*XP2) + T*TSTEP*((1-T*TSTEP)*XP2+T*TSTEP*XP3)
1530 YB1%(T) = (1-T*TSTEP)*((1-T*TSTEP)*YP1 + T*TSTEP*YP2) + T*TSTEP*((1-T*TSTEP)*YP2+T*TSTEP*YP3)
1550 NEXT T
1560 RETURN

1600 ' store B2 curve
1610 FOR T=0 TO BSTEP
1620 XB2%(T) = (1-T*TSTEP)*((1-T*TSTEP)*XP1 + T*TSTEP*XP2) + T*TSTEP*((1-T*TSTEP)*XP2+T*TSTEP*XP3)
1630 YB2%(T) = (1-T*TSTEP)*((1-T*TSTEP)*YP1 + T*TSTEP*YP2) + T*TSTEP*((1-T*TSTEP)*YP2+T*TSTEP*YP3)
1650 NEXT T
1660 RETURN

1700 ' trace pair bezier
1710 FOR T=0 TO BSTEP
1720 LINE(XB1%(T),YB1%(T))-(XB2%(T),YB2%(T)),fcol
1730 NEXT T
1750 RETURN
