00 ' Pixel November ##
01 ' Template

05 MYTITLE$ = "Mondrian"

09 CLS
10 SCREEN 0,7,0
11 ATTRB 1,1
12 LOCATE (40-LEN(MYTITLE$)*2)/2-1, 6, 0
13 PRINT MYTITLE$

14 ATTRB 0,1
15 LOCATE 11, 12, 0
16 PRINT "Pixel November 02"

17 LOCATE 15, 15, 0
18 ATTRB 0,0
19 PRINT "by FloGom"

20 ' Start new seed for random function
21 ATTRB 0,0
22 LOCATE 8, 22, 0
23 PRINT "Push any key to start."
24 A$ = INKEY$
25 U = RND
26 IF A$="" THEN 24

30 CLS
31 LOCATE 0,0,0

49 ' Def cst
50 DEFINT X, Y, W
51 XWIN = 40
52 YWIN = 25
53 THICK = 3
54 DIM CMAP%(4)
55 CMAP(0) = -2 : CMAP(1) = -4 : CMAP(2) = -5 : CMAP(3) = -8
56 DIM LASTWZ%(2)
57 DIM WCOL%(4)
58 LINE (0,0)-(319,0) : LINE (0,1)-(319,1) : LINE (0,2)-(319,2)
59 LINE (0,0)-(0,199) : LINE (1,0)-(1,199) : LINE (2,0)-(2,199)

60 ' 1ere iteration
61 CITER = -1
65 XL = 0 : XR = XWIN : YT = 0 : YB = YWIN
66 XMAR = 5 : YMAR = 3
68 LASTWZ%(0)=-1: LASTWZ%(1)=-1
69 CITER = CITER + 1
70 XC = FIX(RND*(XR-XL-2*XMAR)) + XMAR + XL
80 GOSUB 605
85 YC = FIX(RND*(YB-YT-2*YMAR)) + YMAR + YT
90 GOSUB 655
91 WCOL(0)=3 : WCOL(1)=3 : WCOL(2)=3 : WCOL(3)=3

92 PLAY "PPPPPPPPPPPPPPP"

95 ' choose a random zone and a random color
98 FOR K=1 TO 2
100 WZONE = CINT(RND*3)
101 IF LASTWZ%(0) = WZONE THEN 100
102 IF LASTWZ%(1) = WZONE THEN 100
103 LASTWZ%(K-1) = WZONE
104 WCOL%(K) = CINT(RND*2)
105 NEXT K

106 FOR WZ = 0 TO 3
110 IF WZ = 0 THEN BOXF(XL*8+THICK,YT*8+THICK)-(XC*8-1,YC*8-1),CMAP(WCOL(WZ))
115 IF WZ = 1 THEN BOXF(XC*8+THICK,YT*8+THICK)-(XR*8-1,YC*8-1),CMAP(WCOL(WZ))
120 IF WZ = 2 THEN BOXF(XL*8+THICK,YC*8+THICK)-(XC*8-1,YB*8-1),CMAP(WCOL(WZ))
125 IF WZ = 3 THEN BOXF(XC*8+THICK,YC*8+THICK)-(XR*8-1,YB*8-1),CMAP(WCOL(WZ))
129 PLAY "PPPPPPPPPPPPPPP"
130 NEXT WZ

134 ' choice of a second zone to draw
135 XMAR = 2 : YMAR = 2
145 WZONE = CINT(RND*3)
150 IF WZONE = LASTWZ%(0) THEN 145
155 IF WZONE = LASTWZ%(1) THEN 145

160 IF WZONE = 0 THEN XL = 0 : XR = XC : YT = 0 : YB = YC
165 IF WZONE = 1 THEN XL = XC : XR = XWIN : YT = 0 : YB = YC
170 IF WZONE = 2 THEN XL = 0 : XR = XC : YT = YC : YB = YWIN
175 IF WZONE = 3 THEN XL = XC : XR = XWIN : YT = YC : YB = YWIN


190 PLAY "PPPPPPPPPPPPPPP"
200 IF CITER=0 THEN 69

210 GOTO 60

495 A$=INKEY$
496 IF A$="" THEN 495
497 CLS

500 END

600 ' draw vet line
605 X1 = XC
610 X2=X1
620 FOR T=0 TO THICK-1
630 LINE(X1*8+T,YT*8)-(X2*8+T,YB*8)
635 NEXT T
640 RETURN

650 ' draw horz line
655 Y1 = YC
660 Y2=Y1
670 FOR T=0 TO THICK-1
680 LINE(XL*8,Y1*8+T)-(XR*8,Y2*8+T)
690 NEXT T
695 RETURN
